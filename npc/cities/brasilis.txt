//===== rAthena Script =======================================
//= Brasilis Town
//===== Description: =========================================
//= [Official Conversion]
//= Brasilis Town Script
//===== Changelog: ===========================================
//= 1.0 First version. Transportation and Basic NPCs. [L0ne_W0lf]
//= 1.1 Fixed Zeny not being removed for payment. [Kisuka]
//= 1.2 Updated dialog, added Ice Cream Maker. [L0ne_W0lf]
//= 1.3 Moved Alberta NPC to pre-re/re paths. [Euphy]
//= 1.4 Added VIP features. [Euphy]
//============================================================

// Brasilis Transportation :: brasilis_trans
//============================================================
-	script	::Crewman_bra2	-1,{
	mes "[Tripulante]";
	mes "Ei, você já ouviu falar de um lugar chamado Brasilis?";
	mes "É uma cidade tropical que é quente como o deserto, mas também chuvosa. É um lugar muito misterioso.";
	next;
	mes "[Tripulante]";
	mes "Recentemente encontramos uma nova rota oceânica para chegar lá facilmente.";
	if (VIP_SCRIPT) {
		set .@cost, vip_status(VIP_STATUS_ACTIVE)?1000:10000;
		mes "São apenas 10.000 zeny para uma viagem de ida e volta, e 1.000 para VIP! Então, quer ir?"; //tradução personalizada (VIP)
	} else {
		set .@cost,10000;
		mes "São apenas 10.000 zeny para uma viagem de ida e volta! Então, quer ir?";
	}
	next;
	switch(select("Leve-me para Brasilis!:Vou ficar aqui.")) {
	case 1:
		if (Zeny < .@cost) {
			mes "[Tripulante]";
			mes "Eu disse "+callfunc("F_InsertComma",.@cost)+" zeny.";
			close;
		}
		else {
			mes "[Tripulante]";
			mes "Legal~!! Vamos lá~!";
			set Zeny, Zeny-.@cost;
			close2;
			warp "brasilis",314,60;
			end;
		}
	case 2:
		mes "[Tripulante]";
		mes "Bem, se algum dia estiver interessado, me avise e eu posso te levar para lá.";
		close;
	}
}

brasilis,316,57,3	script	Crewman#bra1	100,{
	mes "[Tripulante]";
	mes "Meu navio está voltando para Alberta, quer vir conosco?";
	next;
	switch(select("Voltar para Alberta.:Ainda não~.")) {
	case 1:
		mes "[Tripulante]";
		mes "Com certeza sinto falta de casa.";
		close2;
		if (checkre(0))
			warp "alberta",243,82;
		else
			warp "alberta",244,115;
		end;
	case 2:
		mes "[Tripulante]";
		mes "Ok, como quiser. Nos veremos quando voltarmos então.";
		close;
	}
}

// NPCs Genéricos de Brasilis :: brazil_tre
//============================================================
brasilis,155,165,3	script	Signpost#bra1	858,{
	mes ":: Museu de Arte ::";
	close;
}

brasilis,191,239,3	script	Signpost#bra2	858,{
	mes ":: Monumento Verass ::";
	close;
}

brasilis,240,247,3	script	Signpost#bra3	858,{
	mes ":: Mercado ::";
	mes " ";
	mes "- Para suas Poções e Armamentos -";
	close;
}

brasilis,303,309,3	script	Signpost#bra4	858,{
	mes ":: Teleférico da Selva ::";
	mes "- Não para os fracos de coração -";
	close;
}

brasilis,278,137,3	script	Signpost#bra5	858,{
	mes ":: Hotel Brasilis ::";
	close;
}

brasilis,137,77,5	script	Ice-Cream Maker	85,{
	mes "[Fabricante de Sorvete]";
	mes "Venha~venha~";
	mes "Sorvete é o lanche perfeito para um dia quente~";
	mes "São apenas ^3355FF100 Zeny^000000~";
	mes "Sorvete~";
	mes "Pegue seu Sorvete!";
	next;
	switch(select("Me dê um!:Sorvete?:Cancelar.")) {
	case 1:
		mes "[Fabricante de Sorvete]";
		mes "Como há tantas pessoas querendo um sorvete refrescante, você só pode pedir 5 por vez.";
		mes "Então, quantos você quer?";
		next;
		while(1) {
			input .@input; //,1,5;
			if (.@input == 0) {
				mes "[Fabricante de Sorvete]";
				mes "Nenhum?";
				mes "Tudo bem, saia do caminho, tenho clientes para atender.";
				close;
			}
			else if ((.@input < 0) || (.@input > 5)) {
				mes "[Fabricante de Sorvete]";
				mes "Uau.";
				mes "Você pediu demais.";
				mes "Se comer mais de 5, pode acabar lutando com um monstro no seu estômago. Se acalme, amigo.";
				next;
			}
			else
				break;
		}
		set .@icecream_hap,.@input*100;
		if (Zeny < .@icecream_hap) {
			mes "[Fabricante de Sorvete]";
			mes "Cara~! Você não tem dinheiro suficiente.";
			mes "São apenas ^3355FF100 Zeny^000000~ Sério!";
			close;
		}
		if (!checkweight(536,.@input)) {
			mes "[Fabricante de Sorvete]";
			mes "Parece que você tem coisas demais.";
			mes "Desocupe um pouco sua mochila antes de comprar.";
			close;
		}
		set Zeny, Zeny-.@icecream_hap;
		getitem 536,.@input; //Ice_Cream
		close;
	case 2:
		mes "[Fabricante de Sorvete]";
		mes "'Sorvete é...";
		mes "Espere, você não sabe";
		mes "o que é Sorvete?";
		mes "Em que caverna você";
		mes "esteve vivendo?";
		next;
		mes "[Fabricante de Sorvete]";
		mes "Nem vou começar a falar sobre como isso soa estranho.";
		mes "De qualquer forma, pegue seu Sorvete aqui enquanto está bem gelado.";
		close;
	case 3:
		mes "[Fabricante de Sorvete]";
		mes "Não perca a chance de provar o melhor Sorvete de toda a terra~!";
		close;
	}
}
